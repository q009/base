tool_actions = []

tool_do_action = [
    doargs (listassoc=s $$arg1 action)
]

TBI_KEY = 0
TBI_KMOD = 1

// 1:<name> 2:<key> 3:<modifiers>
tool_set_bind_info = [
    [tb_@arg1] = [ [@@arg2] @arg3 ]
]

// 1:<name>
tool_get_bind_info = [
    result $[tb_@arg1]
]

// 1:<name>
tool_has_bind_info = [
    result (identexists [tb_@arg1])
]

// 1:<name>
tool_str_bind_info = [
    local bind_info key modifiers
    bind_info = (tool_get_bind_info $arg1)
    key = (at $bind_info @TBI_KEY)
    modifiers = (at $bind_info @TBI_KMOD)

    result (prettybindinfo $key $modifiers)
]

// 1:<name>
toolunbind = [
    local bind_info key modifiers
    bind_info = (tool_get_bind_info $arg1)
    key = (at $bind_info @TBI_KEY)
    modifiers = (at $bind_info @TBI_KMOD)

    if $key [
        editbind $key [] $modifiers
        tool_set_bind_info $arg1 "" 0
    ]
]

// 1:<key> 2:<key modifier mask>
gettoolbind = [
    local binding prefix offset
    prefix = "tool_do_action "
    binding = (geteditbind $arg1 $arg2)
    offset = (strstr $binding $prefix)

    if (> $offset -1) [
        offset = (+ $offset (strlen $prefix))
        binding = (substr $binding $offset)
        result (at $binding 0)
    ]
]

// 1:<key> 2:<key modifier mask> 3:<name>
toolbind = [
    local old_action
    old_action = (gettoolbind $arg1 $arg2)

    if $old_action [
        tool_set_bind_info $old_action "" 0
    ]

    tool_set_bind_info $arg3 $arg1 $arg2

    editbind $arg1 [
        tool_do_action @arg3
    ] $arg2
]

// 1:<name> 2:<properties>
tool_action = [
    local newaction
    newaction = []

    append newaction [name @arg1]
    append newaction $arg2

    if (! (listhas $tool_actions $arg1)) [
        append tool_actions $arg1
    ]

    $arg1 = $newaction

    if (! (tool_has_bind_info $arg1)) [
        tool_set_bind_info $arg1 "" 0
    ]
]

// 1:<action name> 2:<prop>
tool_action_prop = [
    result [ (listassoc=s $@arg1 @arg2) ]
]

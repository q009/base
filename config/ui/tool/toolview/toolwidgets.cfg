// 1:<prop defs> 2:<prop var>
tool_props = [
    if (> (listlen $arg1) 25) [
        echo "Error, too many properties!"
    ] [
        code = [local]

        looplist prop $arg1 [
            append code (at $prop 0)
        ]

        append code ";"

        looplist prop $arg1 [
            append code (format "%1 = %2 ;" (at $prop 0) (at $prop 1))
        ]

        append code (format "doargs $%1" $arg2)

        result $code
    ]
]

tool_tip_content_var = []
tool_tip_simple_text = ""
tool_tip_action_name = ""

ui_tool_tip_content_action = [
    local action_desc action_keys
    action_desc = @(tool_action_prop [$tool_tip_action_name] long_desc)
    action_keys = (tool_str_bind_info $tool_tip_action_name)

    uitext $action_desc $ui_toolview_base_text_size
    if $action_keys [
        uicolourtext $action_keys $ui_toolview_dark_accent_colour $ui_toolview_small_text_size
    ]
]

ui_tool_tip_content_simple = [
    uitext $tool_tip_simple_text $ui_toolview_base_text_size
]

ui_tip_tool = [
    if $tool_tip_content_var [
        $tool_tip_content_var
    ]
]

tool_tip_props = [
    [ uit_tip "" ]
    [ uit_tip_simple "" ]
    [ uit_tip_action "" ]
]

// 1:<props>
ui_tool_tip = [
    @(tool_props $tool_tip_props arg1)

    uihover [
        tool_tip_content_var = []

        caseif (!=s $uit_tip "") [
            tool_tip_content_var = $uit_tip
        ] (!=s $uit_tip_simple "") [
            tool_tip_simple_text = $uit_tip_simple
            tool_tip_content_var = ui_tool_tip_content_simple
        ] (!=s $uit_tip_action "") [
            tool_tip_action_name = $uit_tip_action
            tool_tip_content_var = ui_tool_tip_content_action
        ]
    ]

    if $tool_tip_content_var [
        uitooltip tool
    ]
]

ui_tool_preview_spin = [
    result [
        uipreviewyaw (mod (div (getmillis) 10) 360)
    ]
]

tool_hscrollarea_props = [
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_width [(uiwidth $ui_toolpanel_content_width)] ]
    [ uit_height 0.1 ]
    [ uit_scroll_speed 2 ]
]

// 1:<content> 2:<props>
ui_tool_hscrollarea = [
    @(tool_props $tool_hscrollarea_props arg2)

    local has_scrollbar
    has_scrollbar = 0

    uihlist 0 [
        uiscroll 0 $uit_height [
            doargs @@arg1

            if (>f (uigetclipvirth) $uit_height) [
                has_scrollbar = 1
                uit_width = (-f $uit_width $uit_slider_size)
            ]

            uifill $uit_width
        ]

        if $has_scrollbar [
            uivscroll $uit_slider_size $uit_height $uit_scroll_speed
        ]
    ]
]

// 1:<w> 2:<h> 3:<children> 4:<disabled>
ui_tool_interactable = [
    result [
        uiskin @arg1 @arg2 $ui_toolview_base_colour $ui_toolview_highlight_colour $ui_toolview_select_colour [
            @@arg3
        ] (! @arg4)
    ]
]

// 1:<text> 2:<var> 3:<length> 4:<prompt>
ui_tool_textinput = [
    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $arg1 $ui_toolview_small_text_size [ uialign -1 ]
        uiinput $arg2 $arg3 [] $ui_toolview_small_text_size 1 $arg4
    ]
]

// 1:<label> 2:<var> 3:<step> 4:<props>
ui_tool_var_input = [
    local var_type var_min var_max var_def
    var_type = (getvartype $arg2)

    case $var_type $ididxvar [
        var_min = (getvarmin $arg2)
        var_max = (getvarmax $arg2)
        var_def = (getvardef $arg2)

        ui_tool_numinput $arg2 $var_min $var_max $arg3 [
            uit_val_format = i
            uit_can_reset = 1
            uit_reset_val = @var_def
            uit_label = [@arg1]
            uit_immediate = 0
            @arg4
        ]
    ] $ididxfvar [
        var_min = (getfvarmin $arg2)
        var_max = (getfvarmax $arg2)
        var_def = (getfvardef $arg2)

        ui_tool_numinput $arg2 $var_min $var_max $arg3 [
            uit_can_reset = 1
            uit_reset_val = @var_def
            uit_label = [@arg1]
            uit_immediate = 0
            @arg4
        ]
    ]
]

tool_param_var = []
tool_param_copy_val = 0
tool_param_copied = 0
tool_param_can_reset = 0
tool_param_reset_val = 0
tool_param_on_change = []

// 1:<var> 2:<value> 3:<on change>
tool_param_set = [
    if (!=s $$arg1 $arg2) [
        $arg1 = $arg2
        doargs $arg3
    ]
]

// 1:<var> 2:<delta> 3:<on change>
tool_param_delta = [
    if $arg2 [
        $arg1 = $arg2
        doargs $arg3
    ]
]

tool_param_copy = [
    tool_param_copy_val = $$tool_param_var
    tool_param_copied = 1
]

tool_param_paste = [
    if $tool_param_copied [
        tool_param_set $tool_param_var $tool_param_copy_val $tool_param_on_change
    ]
]

tool_param_reset = [
    if $tool_param_can_reset [
        tool_param_set $tool_param_var $tool_param_reset_val $tool_param_on_change
    ]
]

ui_tool_param_menu = [
    uivlist 0 [
        uiclamp 1 1

        uihover [
            menu_click_hide = 1
        ]

        ui_tool_button [
            uit_label = "Copy value"
            uit_label_align = -1
            uit_on_click = [
                tool_param_copy
            ]
        ]

        local paste_text

        if $tool_param_copied [
            paste_text = (format "Paste value (%1)" $tool_param_copy_val)
        ] [
            paste_text = "Paste value"
        ]

        ui_tool_button [
            uit_disabled = (! $tool_param_copied)
            uit_label = [@@paste_text]
            uit_label_align = -1
            uit_on_click = [
                tool_param_paste
            ]
        ]

        ui_tool_button [
            uit_disabled = (! $tool_param_can_reset)
            uit_label = "Reset value"
            uit_label_align = -1
            uit_on_click = [
                tool_param_reset
            ]
        ]

        uiclamp* 1 1
    ]
]

// 1:<var> 2:<can reset> 3:<reset val> 4:<on change>
tool_param_menu = [
    tool_param_var = $arg1
    tool_param_can_reset = $arg2
    tool_param_reset_val = $arg3
    tool_param_on_change = $arg4

    toolpanel_show tool_param_menu "" menu [
        uit_position = (uicursorpos)
    ]
]

exec "config/ui/tool/toolview/widgets/toolnuminput.cfg"
exec "config/ui/tool/toolview/widgets/tooldropdown.cfg"
exec "config/ui/tool/toolview/widgets/tooltexlist.cfg"
exec "config/ui/tool/toolview/widgets/toolmodellist.cfg"
exec "config/ui/tool/toolview/widgets/toolsearchlist.cfg"
exec "config/ui/tool/toolview/widgets/tooltexselect.cfg"
exec "config/ui/tool/toolview/widgets/toolmodelselect.cfg"
exec "config/ui/tool/toolview/widgets/toolsearchselect.cfg"
exec "config/ui/tool/toolview/widgets/tooltexinfo.cfg"
exec "config/ui/tool/toolview/widgets/toolcolour.cfg"
exec "config/ui/tool/toolview/widgets/toolcollapsegroup.cfg"
exec "config/ui/tool/toolview/widgets/toolswitch.cfg"
exec "config/ui/tool/toolview/widgets/toolbutton.cfg"

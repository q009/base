tool_num_input_drag_var = []
tool_num_input_focus_var = []
tool_num_input_focus = 0
tool_num_input_drag_pivot = 0
tool_num_input_drag_val = 0
tool_num_input_edit = 0
tool_num_input_last_val = 0

// 1:<pivot val> 2:<min> 3:<max> 4:<step>
tool_num_input_drag_f = [
    local distance new_val

    uilockcursor
    uicursortype 2

    distance = (toint (round (*f (uimousetrackx) 100)))

    if (>f $arg4 0) [
        new_val = (+f $arg1 (*f $arg4 $distance))
    ] [
        new_val = (*f $arg1 (divf 1 (pow 2 (*f -1 $distance))))
    ]

    if $uit_circular [
        circclampf $new_val $arg2 $arg3
    ] [
        clampf $new_val $arg2 $arg3
    ]
]

// 1:<pivot val> 2:<min> 3:<max> 4:<step>
tool_num_input_drag_i = [
    local distance new_val

    uilockcursor
    uicursortype 2

    distance = (toint (round (*f (uimousetrackx) 100)))

    if (> $arg4 0) [
        new_val = (+ $arg1 (* $arg4 $distance))
    ] [
        new_val = (* $arg1 (div 1 (pow 2 (* -1 $distance))))
    ]

    if $uit_circular [
        circclamp $new_val $arg2 $arg3
    ] [
        clamp $new_val $arg2 $arg3
    ]
]

tool_numinput_props = [
    [ uit_disabled 0 ]
    [ uit_has_menu 1 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_val_format f ]
    [ uit_circular 0 ]
    [ uit_val_size $ui_toolview_small_text_size ]
    [ uit_immediate 1 ]
    [ uit_label "" ]
    [ uit_label_size $ui_toolview_small_text_size ]
    [ uit_input_length 8 ]
    [ uit_width $ui_toolview_num_input_size ]
    [ uit_height 0 ]
    [ uit_on_change [] ]
    [ uit_delta 0 ]
]

// 1:<var> 2:<min> 3:<max> 4:<step> 5:<props>
ui_tool_numinput = [
    @(tool_props $tool_numinput_props arg5)

    local drag value colour new_value drag_func clamp_func
    drag = (=s $tool_num_input_drag_var $arg1)
    drag_func = (? (=s $uit_val_format f) tool_num_input_drag_f tool_num_input_drag_i)
    clamp_func = (? (=s $uit_val_format f) clampf clamp)

    if $uit_delta [
        arg2 = -999999
        arg3 = 999999
        $arg1 = 0
    ]

    if $drag [
        value = $tool_num_input_drag_val
    ] [
        value = (? $uit_disabled "-" $$arg1)
    ]

    colour = (? $uit_disabled $ui_toolview_dark_colour $ui_toolview_accent_colour)
    new_value = ""

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $uit_label $uit_label_size [ uialign -1 ]

        if (=s $tool_num_input_focus_var $arg1) [
            uiinput tool_num_input_edit $uit_input_length [] $uit_val_size 0 "" 0 "" [
                uieditorsetfocusable 0

                // Focus once
                if $tool_num_input_focus [
                    uieditorsetfocus
                    tool_num_input_focus = 0
                ]

                // Hide this field when done interacting
                if (uifocus?) [] [
                    tool_num_input_focus_var = []
                    new_value = $tool_num_input_edit
                ]
            ] [
                uifill $uit_width
            ]
        ] [
            @@@(ui_tool_interactable [$uit_width] [$uit_height] [
                uispace $ui_padsmall $ui_padsmaller [
                    uialign -1
                    uitriangle $colour 0.005 0.005 90
                ]

                uispace $ui_padsmall $ui_padsmaller [
                    uitext $value $uit_val_size
                ]

                uispace $ui_padsmall $ui_padsmaller [
                    uialign 1
                    uitriangle $colour 0.005 0.005 -90
                ]

                if $uit_disabled [] [
                    uihover [
                        if $drag [
                            tool_num_input_drag_val = ($drag_func $tool_num_input_drag_pivot $arg2 $arg3 $arg4)
                            if (=s $uit_val_format i) [
                                tool_num_input_drag_val = (toint $tool_num_input_drag_val)
                            ]
                            if $uit_immediate [
                                new_value = $tool_num_input_drag_val
                            ]
                        ]
                    ]

                    uidoublepress $arg1 [
                        // Show input field
                        tool_num_input_focus_var = $arg1
                        tool_num_input_focus = 1
                        tool_num_input_edit = $$arg1
                        tool_num_input_last_val = 0

                        if $drag [
                            tool_num_input_drag_var = []
                        ]
                    ]

                    uipress [
                        if (=s $tool_num_input_focus_var $arg1) [] [
                            // Init drag
                            tool_num_input_drag_val = $$arg1
                            tool_num_input_drag_pivot = $$arg1
                            tool_num_input_drag_var = $arg1
                            tool_num_input_last_val = 0
                        ]
                    ]

                    uirelease [
                        if $drag [
                            if $uit_immediate [] [
                                new_value = $tool_num_input_drag_val
                            ]

                            tool_num_input_drag_var = []
                        ]
                    ]

                    uialtrelease [
                        caseif $drag [
                            // Cancel drag
                            tool_num_input_drag_var = []
                            new_value = $tool_num_input_drag_pivot
                        ] $uit_has_menu [
                            tool_param_menu $arg1 $uit_can_reset $uit_reset_val $uit_on_change
                        ]
                    ]
                ]
            ] [$uit_disabled])
        ]
    ]

    if (!=s $new_value "") [
        new_value = ($clamp_func $new_value $arg2 $arg3)
        if $uit_delta [
            local delta
            if (=s $uit_val_format f) [
                delta = (-f $new_value $tool_num_input_last_val)
            ] [
                delta = (- $new_value $tool_num_input_last_val)
            ]
            tool_param_delta $arg1 $delta $uit_on_change
            tool_num_input_last_val = $new_value
        ] [
            tool_param_set $arg1 $new_value $uit_on_change
        ]
    ]
]

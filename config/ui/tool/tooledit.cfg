//===============//
// EDIT SETTINGS //
//===============//

ui_tool_edit_settings = [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        ui_tool_var_input "Undo size (MB):" undomegs 8
        ui_tool_var_input "Float speed:" floatspeed 50 [
            uit_immediate = 1
        ]
        ui_tool_var_input "Fast float speed:" shiftfloatspeed 50 [
            uit_immediate = 1
        ]

        ui_tool_checkbox nompedit [
            uit_label = "Lock non-multiplayer functionality"
        ]

        ui_tool_checkbox entalwaysdrag [
            uit_label = "Always drag entities"
        ]

        uialign* -1 -1
    ]
]

tool_action_edit_settings = [
    toolpanel_toggle tool_edit_settings "Editor settings" left
]

//========//
// SEARCH //
//========//

tool_search_query = ""
tool_search_results = []
tool_search_num_actions = 0
tool_search_action_idx = 0

tool_search_init = [
    tool_search_query = ""
    tool_search_results = []
    tool_search_num_actions = 0
    tool_search_action_idx = 0
]

tool_search_proc = [
    local num_actions
    num_actions = (listlen $tool_actions)
    tool_search_num_actions = 0
    tool_search_action_idx = 0
    tool_search_results = []

    if (strlen $arg1) [
        loop i $num_actions [
            local action
            action = (at $tool_actions $i)

            if (> (strcasestr @@(tool_action_prop [$action] short_desc) $arg1) -1) [
                append tool_search_results $action
                tool_search_num_actions = (+ 1 $tool_search_num_actions)
            ]
        ]
    ]
]

ui_tool_search_input = [
    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uiimage "textures/icons/action" $ui_toolview_accent_colour 0 $ui_toolview_base_icon_size $ui_toolview_base_icon_size
        uiinput tool_search_query 24 [
            tool_search_proc $tool_search_query
        ] $ui_toolpanel_text_size 0 "[search for action here]" 0 "" [
            if $toolpanel_init uieditorsetfocus
        ]
    ]
]

ui_tool_search_list = [
    if $toolpanel_init [
        tool_search_action_idx = -1
    ]

    loop i $tool_search_num_actions [
        local action
        action = (at $tool_search_results $i)

        if (= $i $tool_search_action_idx) uisethighlight
        ui_tool_button [
            uit_icon = @(tool_action_prop [$action] icon)
            uit_label = @(tool_action_prop [$action] short_desc)
            uit_tip_action = $action
            uit_on_click = [
                toolpanel_hide tool_search
                tool_do_action @action
            ]
        ]
        uiclamp- 1 1
    ]
]

tool_search_nav = [
    tool_search_action_idx = (- $tool_search_action_idx $arg2)
    tool_search_action_idx = (clamp $tool_search_action_idx 0 (- $tool_search_num_actions 1))

    if $arg3 [
        tool_do_action (at $tool_search_results $tool_search_action_idx)
        toolpanel_hide tool_search
    ]
]

ui_tool_search = [
    uivlist 0 [
        uiclamp 1 1
        ui_tool_search_input
        uifill 0 $ui_toolpanel_elem_space
        uiline $ui_toolview_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_toolpanel_elem_space
        ui_tool_search_list
    ]
]

tool_action_search = [
    if (toolpanel_getpanel tool_search) [] [
        tool_search_init
    ]

    toolpanel_show tool_search "Search action" mid [
        uit_nav_enable = 1
    ]
]

tool_action ta_menu [
    short_desc "Main menu"
    long_desc "Open main menu"
    icon "textures/icon"
    action [
        // TODO: Open menu here
    ]
]

tool_action ta_undo [
    short_desc "Undo"
    long_desc "Undo last edit"
    icon "textures/icons/menu"
    action [
        undo
    ]
]

tool_action ta_redo [
    short_desc "Redo"
    long_desc "Redo last edit"
    icon "textures/icons/menu"
    action [
        redo
    ]
]

tool_action ta_copy [
    short_desc "Copy"
    long_desc "Copy"
    icon "textures/icons/menu"
    action [
        editcopy
    ]
]

tool_action ta_paste [
    short_desc "Paste"
    long_desc "Paste"
    icon "textures/icons/menu"
    action [
        editpaste
    ]
]

tool_action ta_edit_settings [
    short_desc "Editor settings"
    long_desc "Change map editor settings"
    icon "textures/icons/settings"
    action [
        tool_action_edit_settings
    ]
]

tool_action ta_search [
    short_desc "Search action"
    long_desc "Search for an editor action"
    icon "textures/icons/question"
    action [
        tool_action_search
    ]
]

exec "config/ui/tool/toolentparam.cfg"

// 1:<type> 2:<attr name list> 3:<subtype>
ui_tool_ent_attr_props = [
    local attr_list nodelta disable
    attr_list = []
    nodelta = 0

    looplist attr_name $arg2 [
        append attr_list (tool_ent_attr_idx_map $arg1 $attr_name $arg3)
        if (& (tool_ent_aflags $arg1 $attr_name) $T_ENT_NODELTA) [
            nodelta = 1
        ]
    ]

    disable = ""
    if $nodelta [
        disable = "uit_disabled = (tool_ent_delta_edit)"
    ]

    result [
        uit_label_size = $ui_toolview_tiny_text_size
        uit_on_change = [
            tool_ent_attr_change @@arg1 [@@@attr_list]
        ]
        uit_can_reset = 1
        uit_reset_val = 0
        @disable
        uit_has_menu = (! (tool_ent_delta_edit))
        uit_delta = (tool_ent_delta_edit)
    ]
]

// 1:<text> 2:<fields>
ui_tool_ent_param_group = [
    uihlist 0 [
        uiclamp 1 1
        uitext $arg1 $ui_toolview_small_text_size
        uivlist 0 [
            @arg2
        ]
    ]
    uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
]

// 1:<type> 2:<subtype> 3:<suffix>
ui_tool_ent_palette_group = [
    result [
        ui_tool_ent_param_group [Palette @@arg3] [
            ui_tool_dropdown @@(tool_ent_attr $arg1 [palette@arg3] $arg2) [
                "Pulse"
                "Team"
                "Weapon"
            ] [
                @@@(ui_tool_ent_attr_props $arg1 [palette@arg3] $arg2)
                uit_label = "Type"
            ]

            ui_tool_dropdown tool_ent_attr_palindex@@[arg3]_val (at $tool_palette_ids $@@(tool_ent_attr $arg1 [palette@arg3] $arg2)) [
                @@@(ui_tool_ent_attr_props $arg1 [palindex@arg3] $arg2)
                uit_label = "Id"
            ]

            uifill 0 $ui_toolview_mid_elem_space

            ui_tool_checkbox tool_ent_attr_palindex@@[arg3]_enforce [
                uit_label = "Enforce"
                uit_label_size = $ui_toolview_tiny_text_size
                @@@(ui_tool_ent_attr_props $arg1 [palindex@arg3] $arg2)
            ]
            uialign- 1
        ]
    ]
]

// 1:<type> 2:<fx>
ui_tool_ent_variant_group = [
    local fxlevel_group fxlevel_var

    if $arg2 [
        fxlevel_group = [
            uihlist 0 [
                uitext "FX Level:" $ui_toolview_tiny_text_size
                uifill $ui_toolview_base_elem_space
                loop i 3 [
                    fxlevel_var = [tool_ent_attr_fxlevel_@(+ $i 1)]
                    ui_tool_compactswitch $fxlevel_var [
                        @@@@(ui_tool_ent_attr_props $arg1 fxlevel $arg2)
                        uit_label = @(+ $i 1)
                        uit_width = 0.02
                        uit_height = 0.02
                    ]
                ]
            ]
            uialign- 1
        ]
    ]

    result [
        ui_tool_ent_param_group "Variant" [
            ui_tool_dropdown @@(tool_ent_attr $arg1 variant $arg2) [
                "All"
                "Default"
                "Alternate"
            ] [
                @@@(ui_tool_ent_attr_props $arg1 variant $arg2)
                uit_label = "Map variant"
            ]

            @@fxlevel_group
        ]
    ]
]

// 1:<type>
ui_tool_ent_gamemode_group = [
    result [
        ui_tool_ent_param_group "Game mode" [
            local attr_mode

            uigrid 3 0 0 [
                loop i (listlen $tool_modes) [
                    attr_mode = (at $tool_modes $i)
                    ui_tool_compactswitch [tool_ent_attr_modes_@attr_mode] [
                        @@@@@(ui_tool_ent_attr_props $arg1 modes $arg2)
                        uit_label = @attr_mode
                        uit_tip_simple = [@(at $modename (+ $i 2))]
                    ]
                    uiclamp- 1 1
                ]
            ]
        ]

        ui_tool_ent_param_group "Game mutators" [
            local attr_mut

            uigrid 3 0 0 [
                loop i (listlen $tool_muts) [
                    attr_mut = (at $tool_muts $i)
                    ui_tool_compactswitch [tool_ent_attr_muts_@attr_mut] [
                        @@@@@(ui_tool_ent_attr_props $arg1 muts $arg2)
                        uit_label = @attr_mut
                        uit_tip_simple = [@(at $mutsname $i)]
                    ]
                    uiclamp- 1 1
                ]
            ]
        ]
    ]
]

// 1:<type> 2:<flags> 3:<labels> 4:<attr> 5:<title>
ui_tool_ent_flags_group = [
    local code label var attr_name props
    code = ""
    attr_name = (? $arg4 $arg4 flags)

    append code (concatword "ui_tool_ent_param_group ^"" (? $arg5 $arg5 "Flags") "^" [^n")
    append code "uigrid 2 $ui_toolview_base_elem_space $ui_toolview_small_elem_space [^n"
    append code "uiclamp 1 1^n"

    props = (ui_tool_ent_attr_props $arg1 $attr_name $arg2)

    loop i (listlen $arg2) [
        label = (at $arg3 $i)
        var = [tool_ent_attr_@[attr_name]_@(at $arg2 $i)]

        append code (concatword "ui_tool_checkbox " $var " [^n")
        append code (concatword "uit_label = [" $label "]")
        append code $props
        append code "uit_label_size = $ui_toolview_tiny_text_size"
        append code "^n]^n"
    ]

    append code "uialign* -1 -1^n"
    append code "]^n]^n"

    result $code
]

exec "config/ui/tool/ents/toollight.cfg"
exec "config/ui/tool/ents/toolmapmodel.cfg"
exec "config/ui/tool/ents/toolplayerstart.cfg"
exec "config/ui/tool/ents/toolenvmap.cfg"
exec "config/ui/tool/ents/toolparticles.cfg"
exec "config/ui/tool/ents/toolsound.cfg"
exec "config/ui/tool/ents/toollightfx.cfg"
exec "config/ui/tool/ents/tooldecal.cfg"
exec "config/ui/tool/ents/toolwind.cfg"
exec "config/ui/tool/ents/toolweapon.cfg"
exec "config/ui/tool/ents/toolteleport.cfg"
exec "config/ui/tool/ents/toolactor.cfg"
exec "config/ui/tool/ents/tooltrigger.cfg"
exec "config/ui/tool/ents/toolpusher.cfg"
exec "config/ui/tool/ents/toolaffinity.cfg"
exec "config/ui/tool/ents/toolcheckpoint.cfg"
exec "config/ui/tool/ents/toolroute.cfg"
exec "config/ui/tool/ents/toolrail.cfg"
exec "config/ui/tool/ents/toolcamera.cfg"

ui_tool_ent_none = []
ui_tool_ent_outline = []

ui_tool_ent_cur = [
    if (&& $tool_ent_compatible tool_ent_has_sel) [
        ui_tool_ent_@tool_ent_type
    ]
]

tool_ent_list_height = 0.08
tool_last_sel_ents = 0

ui_tool_ent_list = [
    local sel_ents sel_idx text_col
    sel_ents = (getenginestat 15)
    sel_idx = 0

    uitext (concat $sel_ents (? (= $sel_ents 1) "entity" "entities") "selected") $ui_toolview_small_text_size
    uialign- -1 -2

    uicolour $ui_toolview_dark_colour 0 0 [
        uiclamp 1 1

        ui_tool_hscrollarea [
            uivlist 0 [
                uiclamp 1 1

                entloopread [
                    text_col = (? (= $sel_idx (- $sel_ents 1)) $ui_toolview_accent_colour $ui_toolview_dark_accent_colour)

                    ui_tool_button [
                        uit_label = (entget)
                        uit_label_align = -1
                        uit_label_size = $ui_toolview_tiny_text_size
                        uit_colour = $text_col
                        uit_on_click = [
                            entautoview @sel_idx 1
                        ]
                        uit_tip_simple = "Click to view"
                    ]

                    sel_idx = (+ $sel_idx 1)
                ]
                uiclamp* 1 1
            ]

            if (!= $tool_last_sel_ents $sel_ents) [
                uiclipoffsety 1
            ]
        ] [
            uit_height = $tool_ent_list_height
            uit_scroll_speed = 0.2
        ]
    ]

    tool_last_sel_ents = $sel_ents
]

ui_tool_ent_controls = [
    uifill 0 0 [
        uiclamp 1 1
        uihlist 0 [
            ui_tool_button [
                uit_icon = @(tool_action_prop ta_ent_sel_links icon)
                uit_tip_action = ta_ent_sel_links
                uit_on_click = [
                    tool_do_action ta_ent_sel_links
                ]
                uit_width = 0.03
                uit_height = 0.03
                uit_disabled = (! (tool_ent_has_sel))
            ]

            ui_tool_button [
                uit_icon = @(tool_action_prop ta_ent_unify icon)
                uit_tip_action = ta_ent_unify
                uit_on_click = [
                    tool_do_action ta_ent_unify
                ]
                uit_width = 0.03
                uit_height = 0.03
            ]
        ]
        uialign- -1 -2

        ui_tool_compactswitch tool_ent_delta_edit_on [
            uit_label = "Inc"
            uit_tip_simple = "Toggle incremental multi-editing"
            uit_width = 0.03
            uit_height = 0.03
        ]
        uialign- 1 -2
    ]
]

tool_ent_unify = 0
tool_ent_unify_type = 0
tool_ent_unify_mask = 0

tool_ent_unify_perform = [
    local type_idx num_attrs
    type_idx = (indexof $tool_ent_types $tool_ent_type)
    num_attrs = (entityattrs $type_idx)

    entloop [
        if $tool_ent_unify_type [
            enttype $tool_ent_type
        ]

        if (=s (enttype) $tool_ent_type) [
            loop attridx $num_attrs [
                if (& $tool_ent_unify_mask (<< 1 $attridx)) [
                    entattr $attridx $[tool_ent_attr_@attridx]
                ]
            ]
        ]
    ]
]

ui_tool_ent_unify = [
    uitext "Unify entities"

    local type_idx num_attrs
    type_idx = (indexof $tool_ent_types $tool_ent_type)
    num_attrs = (entityattrs $type_idx)

    if (tool_ent_multiple) [
        uihlist $ui_toolview_base_elem_space [
            ui_tool_button [
                uit_label = "Select all"
                uit_on_click = [
                    tool_ent_unify_mask = 0xFFFFFF
                ]
            ]
            ui_tool_button [
                uit_label = "Deselect all"
                uit_on_click = [
                    tool_ent_unify_mask = 0
                ]
            ]
        ]

        uigrid 3 $ui_toolview_base_elem_space $ui_toolview_small_elem_space [
            loop attridx $num_attrs [
                ui_tool_checkbox tool_ent_unify_mask [
                    uit_label = (getentattr $type_idx @attridx $tool_ent_attr_0)
                    uit_check = [
                        & $tool_ent_unify_mask (<< 1 @@attridx)
                    ]
                    uit_set = [
                        ^ $arg1 (<< 1 @@attridx)
                    ]
                ]
            ]
        ]

        uihlist 0 [
            ui_tool_switch tool_ent_unify_type [
                uit_label = "Ent type differences"
                uit_options = ["Skip" "Change"]
                uit_tip_simple = "Whether entities of different types^nshould be skipped or have their types changed"
            ]
        ]

        uihlist $ui_toolview_base_elem_space [
            ui_tool_button [
                uit_label = "Unify"
                uit_label_size = $ui_toolview_base_text_size
                uit_on_click = [
                    tool_ent_unify_perform
                    tool_ent_unify = 0
                ]
            ]
            ui_tool_button [
                uit_label = "Cancel"
                uit_label_size = $ui_toolview_base_text_size
                uit_on_click = [
                    tool_ent_unify = 0
                ]
            ]
        ]
    ] [
        uicolourtext "Must have at least two entities selected" $ui_toolview_warn_colour $ui_toolview_small_text_size
    ]
]

ui_tool_ent = [
    tool_ent_sync

    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1 1 1

        ui_tool_checkbox entselsnap [
            uit_label = "Snap entities to grid"
        ]
        uialign- -1

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_ent_list
        ui_tool_ent_controls

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        if $tool_ent_unify [
            ui_tool_ent_unify
        ] [
            ui_tool_ent_cur
        ]
    ]
]

tool_action_ents = [
    toolpanel_toggle tool_ent "Entities" right
]

tool_action_ent_unify = [
    tool_ent_unify = (! $tool_ent_unify)
    if $tool_ent_unify [
        tool_ent_unify_mask = 0
    ]
]

tool_action_ent_sel_links = [
    selentlinks
]

tool_action ta_ents [
    short_desc "Entities panel"
    long_desc "Open the entities panel"
    icon "textures/icons/editing"
    action [
        tool_action_ents
    ]
]

tool_action ta_ent_sel_links [
    short_desc "Selected linked entities"
    long_desc "Select the entire chain of linked entities"
    icon "textures/icons/shock"
    action [
        tool_action_ent_sel_links
    ]
]

tool_action ta_ent_unify [
    short_desc "Unify entities"
    long_desc "Unify properties of all selected entities"
    icon "textures/icons/editing"
    action [
        tool_action_ent_unify
    ]
]
